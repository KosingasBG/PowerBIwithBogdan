GREY Color - If you want to change color update HEX code in lines 4 VAR LineColor and 8 and 9 stop stop color
Opaque Area Sparkline (no one value) bright = 
// Static line color - use %23 instead of # for Firefox compatibility (Measure Derived from Eldersveld Modified by Kolosko)
VAR LineColour = "%23666666"
VAR PointColour = "white"
VAR Defs = "<defs>
    <linearGradient id='grad' x1='0' y1='25' x2='0' y2='50' gradientUnits='userSpaceOnUse'>
        <stop stop-color='#666666' offset='0' />
        <stop stop-color='#666666' offset='0.3' />
        <stop stop-color='white' offset='1' />
    </linearGradient>
</defs>"
// "Date" field used in this example along the X axis
VAR XMinDate = MIN('Calendar Table'[Month Start])
VAR XMaxDate = MAX('Calendar Table'[Month Start])

// Obtain overall min and overall max measure values when evaluated for each date
VAR YMinValue = MINX(Values('Calendar Table'[Month Start]),CALCULATE([Total Sales]))
VAR YMaxValue = MAXX(Values('Calendar Table'[Month Start]),CALCULATE([Total Sales]))

// Build table of X & Y coordinates and fit to 50 x 150 viewbox
VAR SparklineTable = ADDCOLUMNS(
    SUMMARIZE('Calendar Table','Calendar Table'[Month Start]),
        "X",INT(150 * DIVIDE('Calendar Table'[Month Start]- XMinDate, XMaxDate - XMinDate)),
        "Y",INT(50 * DIVIDE([Total Sales] - YMinValue,YMaxValue - YMinValue)))

// Concatenate X & Y coordinates to build the sparkline
VAR Lines = CONCATENATEX(SparklineTable,[X] & "," & 50-[Y]," ",'Calendar Table'[Month Start])

// Last data points on the line
VAR LastSparkYValue = MAXX( FILTER(SparklineTable, 'Calendar Table'[Month Start] = XMaxDate), [Y])
VAR LastSparkXValue = MAXX( FILTER(SparklineTable, 'Calendar Table'[Month Start] = XMaxDate), [X])

// Add to SVG, and verify Data Category is set to Image URL for this measure
VAR SVGImageURL = 
    "data:image/svg+xml;utf8," & 
    --- gradient---
    "<svg xmlns='http://www.w3.org/2000/svg' x='0px' y='0px' viewBox='-7 -7 164 64'>" & Defs & 
     "<polyline fill='url(#grad)' fill-opacity='0.3' stroke='transparent' 
      stroke-width='0' points=' 0 50 " & Lines & 
      " 150 150 Z '/>" &
    --- Lines---
    "<polyline 
        fill='transparent' stroke='" & LineColour & "' 
        stroke-linecap='round' stroke-linejoin='round' 
        stroke-width='3' points=' " & Lines & 
      " '/>" &
    --- Last Point---
        "<circle cx='"& LastSparkXValue & "' cy='" & 50 - LastSparkYValue & "' r='4' stroke='" & LineColour & "' stroke-width='3' fill='" & PointColour & "' />" &
        "</svg>"
RETURN SVGImageURL
